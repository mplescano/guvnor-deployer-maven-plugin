//created on: 08/04/2016
package com.sample.building

//list any import classes here.
import com.sample.domain.building.*;


//declare any global variables here




rule "When there is a fire then turn on the sprinkler"

    when
        //conditions
        Fire( $room : room )
        $sprinkler : Sprinkler( room == $room, on == false )
    then
        //actions
        modify($sprinkler){ setOn(true) };
        System.out.println("Turn on the sprinkler for room " + $room.getName());
end

rule "When the fire is gone then turn off the sprinkler"
    //include attributes such as "salience" here...
    when
        //conditions
        $room : Room()
        $sprinkler : Sprinkler( room == $room, on == true )
        not Fire( room == $room )
    then
        //actions
        modify($sprinkler){ setOn(false)};
        System.out.println("Turn off the sprinkler for room "+ $room.getName());
end

rule "Raise the alarm when we have one or more fires"
	when
		exists Fire(  )
	then
		insert(new Alarm());
		System.out.println("Raise the alarm");
end

rule "Cancel the alarm when all the fires have gone"
	when
		not Fire(  )
		$alarm : Alarm(  )
	then
		retract($alarm);
		System.out.println("Cancel the alarm");
end

rule "Status output when things are ok"
	when
		not Alarm(  )
		not Sprinkler( on == true )
	then
		System.out.println("Everything is ok");
end